(* zkLogin Protocol Model for ProVerif 2.05 - CORRECTED *)
(* Author: AIT BEN IYCHE Omar *)

type salt_t.
type sub_t.
type aud_t.
type iss_t.
type vku_t.
type exp_t.
type nonce_t.
type usercomp_t.
type zkproof_t.

free c_client: channel.
free c_server: channel.

free aud: aud_t.
free iss: iss_t.
free vku: vku_t.
free T_exp: exp_t.
free accept: bitstring.
free reject: bitstring.

fun poseidon4(sub_t, aud_t, iss_t, salt_t): usercomp_t.
fun poseidon3(vku_t, exp_t, nonce_t): nonce_t.
fun zkproof(sub_t, aud_t, iss_t, salt_t, vku_t, exp_t, nonce_t): zkproof_t [private].

reduc forall sub: sub_t, aud: aud_t, iss: iss_t, salt: salt_t, vku: vku_t, T_exp: exp_t, r: nonce_t;
  verify(poseidon4(sub, aud, iss, salt), poseidon3(vku, T_exp, r), zkproof(sub, aud, iss, salt, vku, T_exp, r)) = true.

event ClientStarted(sub_t).
event ServerAccepted(usercomp_t).
event ClientCommit(sub_t, salt_t, usercomp_t).

let process_client(sub: sub_t) =
  new salt: salt_t;
  new r: nonce_t;
  let user_comp = poseidon4(sub, aud, iss, salt) in
  let n = poseidon3(vku, T_exp, r) in
  let zkp = zkproof(sub, aud, iss, salt, vku, T_exp, r) in
  event ClientStarted(sub);
  event ClientCommit(sub, salt, user_comp);
  out(c_client, (user_comp, n, zkp)).

let process_server() =
  in(c_client, (user_comp: usercomp_t, n: nonce_t, zkp: zkproof_t));
  if verify(user_comp, n, zkp) = true then (
    event ServerAccepted(user_comp);
    out(c_server, accept)
  ).

query u: sub_t, s: salt_t, uc: usercomp_t;
  event(ServerAccepted(uc)) ==> event(ClientCommit(u, s, uc)).

query u: sub_t, s: salt_t, uc: usercomp_t;
  inj-event(ServerAccepted(uc)) ==> inj-event(ClientCommit(u, s, uc)).

process
  new sub: sub_t;
  ((!process_client(sub)) | (!process_server()))
